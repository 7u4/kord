buildscript {
    ext {
        kotlin_version = '1.3.50'
        bintray_version = '1.8.1'
    }
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:5.1.0"
    }
}

ext {
    lib_version = '0.2.0'
    lib_group = 'com.gitlab.kordlib.kord'
    serialization_version = '0.12.0'
    ktor_version = '1.2.2'
    junit_version = '5.5.0-RC1'
    coroutines_version = '1.3.0-M2'
    mockk_version = '1.9.3'
    kotlin_logging_version = '1.6.26'
    log4j_version = '1.7.26'
}

group lib_group
version lib_version

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'kotlinx-serialization'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'
    apply plugin: "com.github.johnrengelman.shadow"

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
        api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
        api "io.github.microutils:kotlin-logging:$kotlin_logging_version"
        testImplementation "io.mockk:mockk-dsl-jvm:$mockk_version"
        testImplementation "io.mockk:mockk:$mockk_version"
        testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlin_version"
        testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
        testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
        testRuntimeOnly "org.slf4j:slf4j-simple:$log4j_version"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    archivesBaseName = "kord-$project.name"

    publishing {
        publications {
            Kord(MavenPublication) {
                from components.kotlin
                groupId lib_group
                artifactId archivesBaseName
                version lib_version

                artifact shadowJar
                artifact sourcesJar
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = ['Kord']
        pkg {
            repo = 'Kord'
            name = 'Kord'
            userOrg = 'kordlib'
            licenses = ['MIT']
            vcsUrl = 'https://gitlab.com/kordlib/kord.git'
            websiteUrl = 'https://gitlab.com/kordlib/kord.git'
            issueTrackerUrl = 'https://gitlab.com/kordlib/kord/issues'

            version {
                name = lib_version
                desc = "Idiomatic Kotlin Wrapper for The Discord API"
                released = new Date()
                vcsTag = lib_version
            }
        }
    }
}
